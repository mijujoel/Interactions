;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
to setup-env
  ;all patches set to default initially - intensity not used on black patches in current version of model
  ask patches [
    set pcolor tech0-color
    set intensity (random 100)
    set original-color tech0-color
  ]

  ;Mass media center for first new technology if turned on by switch - Set up as a square of color violet
  if MASS-MARKET-1? [
    let x int(x-adopt / 100 * max-pxcor)
    let y int(y-adopt / 100 * max-pycor)
    let s int(s-adopt / 100 * (max-pxcor + 1))
    ask patches [
      if (pxcor > x - s) and (pxcor < x + s)
         and (pycor > y - s) and (pycor < y + s) [
         set pcolor tech1-color
         set original-color tech1-color
      ]
    ]
  ]

  ;Mass media center for second new technology (if switch turned on) to challenge first technological innovation - magenta square
  if MASS-MARKET-2? [
    let x int(x-disrupt / 100 * max-pxcor)
    let y int(y-disrupt / 100 * max-pycor)
    let s int(s-disrupt / 100 * (max-pxcor + 1))
    ask patches [
      if (pxcor > x - s) and (pxcor < x + s)
         and (pycor > y - s) and (pycor < y + s) [
         set pcolor tech2-color
         set original-color tech2-color
      ]
    ]
  ]

  ; Patch intensity is randomly assigned to each patch.  To make more realistic,
  ; use the diffuse function to spread the value of each intensity to its nearest
  ; neighbors; the value 1 is the max. diffusion coefficient.  Repeat this
  ; diffusion step "Smoothness" times (set by user) to create a smooth topology.
  ; This applies to all patches.
  ;
  ; Rescale is like converting between degrees Centigrade and Fahrenheit - scale
  ; range from min to max => 0 to 100.  Then recolor the patches.  Only do this for
  ; non-black patches.  Thus, the intensity topology exists over the entire grid
  ; but it is only observed and used in the external patch areas.
  if Smoothness > 0 [
    repeat Smoothness [ diffuse intensity 1 ] ;smoothness function
    rescale
    recolor
  ]

end

to rescale ;adapted from fitness model in Netlogo Community Models
   let highest max [ intensity ] of patches
   let lowest min [ intensity ] of patches
   ask patches [
     set intensity (((intensity - lowest) * 100) / (highest - lowest))
   ]
   let nhighest max [ intensity ] of patches
   let nlowest min [ intensity ] of patches
   print (word "intensity range from " lowest " to " highest " shifted to " nlowest " to " nhighest)

end

to recolor
   ask patches [
       ifelse original-color = tech1-color
          [ set pcolor scale-color tech1-color intensity 0 100 ]
          [ ifelse original-color = tech2-color
               [ set pcolor scale-color tech2-color intensity 0 100 ]
               [ set pcolor tech0-color ] ;ignore for black patches
          ]
   ]
end